%agent: mtrC(dom1, dom2, dom3) # dom1 connects to Fe2O3, the others to flavins, we assume mtrC always has enough electrons
%agent: Flavin(conn, carry~0~2) # we assume Flavin either has 0 or 2 electrons at any time
%agent: Iron(conn, carry~0~2~10)  # this is a physical unit of iron containing any number of molecules. init size depends on size of unit as in Jensen study.

%var: 'Flavin_count' 3009554 # 150477 338574 526672 902866
%var: 'Iron_count' 9104 # 6474797558491

%var: 'Flavin_mtrC_on' 1
%var: 'Flavin_mtrC_off' 1000000
%var: 'Flavin_Iron_on' 1
%var: 'Flavin_Iron_off' 100000
%var: 'Iron_mtrC_on' 1
%var: 'Iron_mtrC_off' 10000

'mtrC connects to Flavin at dom2' mtrC(dom2), Flavin(conn, carry~0) -> mtrC(dom2!1), Flavin(conn!1, carry~0) @ 'Flavin_mtrC_on'
'mtrC connects to Flavin at dom3' mtrC(dom3), Flavin(conn, carry~0) -> mtrC(dom3!1), Flavin(conn!1, carry~0) @ 'Flavin_mtrC_on'
'mtrC disconnects from Flavin at dom2' mtrC(dom2!1), Flavin(conn!1, carry~0) -> mtrC(dom2), Flavin(conn, carry~2) @ 'Flavin_mtrC_off'
'mtrC disconnects from Flavin at dom3' mtrC(dom3!1), Flavin(conn!1, carry~0) -> mtrC(dom3), Flavin(conn, carry~2) @ 'Flavin_mtrC_off'
'mtrC connects to free Iron' mtrC(dom1), Iron(conn, carry~0) -> mtrC(dom1!1), Iron(conn!1, carry~0) @ 1
'mtrC disconnects from Iron' mtrC(dom1!1), Iron(conn!1, carry~0) -> mtrC(dom1), Iron(conn, carry~10), Iron(conn, carry~0) @ 10000 # by setting carry to 10 we effectively remove that iron agent from the game (this is so we can keep count), therefore we introduce a new one, cause the actual number should stay constant
'Flavin connects to free Iron' Iron(conn, carry~0), Flavin(conn, carry~2) -> Iron(conn!2, carry~0),Flavin(conn!2, carry~2) @ 'Flavin_Iron_on'
#'Flavin without electrons disconnects from Iron' Flavin(conn!1, carry~0), Iron(conn!1) -> Flavin(conn, carry~0), Iron(conn) @ 'Flavin_Iron_off'
'Flavin with electrons disconnects from Iron' Flavin(conn!1, carry~2), Iron(conn!1,carry) -> Flavin(conn, carry~0), Iron(conn, carry~2), Iron(conn, carry~0) @ 'Flavin_Iron_off'

%init: 75 mtrC(dom1, dom2, dom3)
%init: 'Flavin_count' Flavin(conn, carry~0)
%init: 'Iron_count' Iron(conn, carry~0)

%var: 'free Iron 2' Iron(conn, carry~2)
%var: 'free Iron 10' Iron(conn, carry~10)

%obs: 'free Iron' Iron(conn, carry~0)
%obs: 'electrons from Flavin' 'free Iron 2' * 2
%obs: 'electrons from mtrC' 'free Iron 10' * 10
%obs: 'Connected Iron' Iron(conn!_)
%obs: 'Connected Flavin' Flavin(conn!_)
