%agent: mtrC(dom1, dom2, dom3) # dom1 connects to Fe2O3, the others to flavins, we assume mtrC always has enough electrons
%agent: Flavin(conn, carry~0~2) # we assume Flavin either has 0 or 2 electrons at any time
%agent: InsolubleIron(conn, carry~0~2~10)  # this is a physical unit of InsolubleIron containing any number of molecules. init size depends on size of unit as in Jensen study.

%var: 'Flavin_count' 2167920 # 361319 812969 1264620 2167920
%var: 'InsolubleIron_count' 582 # 582 3316688360

%var: 'Flavin_mtrC_on' 0.02
%var: 'Flavin_mtrC_off' 50000
%var: 'Flavin_InsolubleIron_on' 2
%var: 'Flavin_InsolubleIron_off' 200000
%var: 'InsolubleIron_mtrC_on' 2
%var: 'InsolubleIron_mtrC_off' 1200

'mtrC connects to Flavin at dom2' mtrC(dom2), Flavin(conn, carry~0) -> mtrC(dom2!1), Flavin(conn!1, carry~0) @ 'Flavin_mtrC_on'
'mtrC connects to Flavin at dom3' mtrC(dom3), Flavin(conn, carry~0) -> mtrC(dom3!1), Flavin(conn!1, carry~0) @ 'Flavin_mtrC_on'
'mtrC disconnects from Flavin at dom2' mtrC(dom2!1), Flavin(conn!1, carry~0) -> mtrC(dom2), Flavin(conn, carry~2) @ 'Flavin_mtrC_off'
'mtrC disconnects from Flavin at dom3' mtrC(dom3!1), Flavin(conn!1, carry~0) -> mtrC(dom3), Flavin(conn, carry~2) @ 'Flavin_mtrC_off'
'mtrC connects to free InsolubleIron' mtrC(dom1), InsolubleIron(conn, carry~0) -> mtrC(dom1!1), InsolubleIron(conn!1, carry~0) @ 'InsolubleIron_mtrC_on'
'mtrC disconnects from InsolubleIron' mtrC(dom1!1), InsolubleIron(conn!1, carry~0) -> mtrC(dom1), InsolubleIron(conn, carry~10), InsolubleIron(conn, carry~0) @ 'InsolubleIron_mtrC_off' # by setting carry to 10 we effectively remove that InsolubleIron agent from the game (this is so we can keep count), therefore we introduce a new one, cause the actual number should stay constant
'Flavin connects to free InsolubleIron' InsolubleIron(conn, carry~0), Flavin(conn, carry~2) -> InsolubleIron(conn!2, carry~0),Flavin(conn!2, carry~2) @ 'Flavin_InsolubleIron_on'
'Flavin with electrons disconnects from InsolubleIron' Flavin(conn!1, carry~2), InsolubleIron(conn!1,carry) -> Flavin(conn, carry~0), InsolubleIron(conn, carry~2), InsolubleIron(conn, carry~0) @ 'Flavin_InsolubleIron_off'

%init: 75 mtrC(dom1, dom2, dom3)
%init: 'Flavin_count' Flavin(conn, carry~0)
%init: 'InsolubleIron_count' InsolubleIron(conn, carry~0)

%var: 'free InsolubleIron 2' InsolubleIron(conn, carry~2)
%var: 'free InsolubleIron 10' InsolubleIron(conn, carry~10)

#%obs: 'free InsolubleIron' InsolubleIron(conn, carry~0)
#%obs: 'Flavin with electrons' Flavin(conn,carry~2)
%obs: 'electrons from 1.2 micromoles Flavin' 'free InsolubleIron 2' * 2
%obs: 'electrons from mtrC' 'free InsolubleIron 10' * 10
#%obs: 'Connected InsolubleIron' InsolubleIron(conn!_)
#%obs: 'Connected Flavin' Flavin(conn!_)

